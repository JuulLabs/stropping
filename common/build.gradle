apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.github.dcendents.android-maven'
apply from: rootProject.file('gradle/jacoco-android.gradle')

group=metadata.groupId

android {
    compileSdkVersion versions.sdk.compile
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.sdk.min
        targetSdkVersion versions.sdk.target
        versionCode 1
        versionName versions.stropping
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = versions.jvmTarget
    }
}

project.afterEvaluate {
    publishing {
        publications {
            library(MavenPublication) {
                setGroupId metadata.groupId
                setArtifactId 'common'
                version android.defaultConfig.versionName
                artifact bundleReleaseAar

                pom.withXml {
                    def dependencies = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each { dependency ->
                        addDependency(dependencies, 'runtime', dependency)
                    }
                    configurations.api.allDependencies.each { dependency ->
                        addDependency(dependencies, 'compile', dependency)
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.kotlin.stdlib
    implementation deps.kotlin.reflect

    testImplementation deps.junit
    testImplementation deps.kotlin.test.junit
}
